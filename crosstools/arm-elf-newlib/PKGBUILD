# Maintainer: Andreas Messer <andi@surveycorner.de>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname=arm-elf-newlib
pkgver=1.20.0
pkgrel=1
pkgdesc="Newlib is a C library intended for use on embedded systems."
arch=('any')
groups=('devel')
url="http://sourceware.org/newlib/"
license=('BSD')
depends=('arm-elf-binutils' 'arm-elf-gcc-base')
replaces=('cross-arm-elf-newlib')
source=(ftp://sources.redhat.com/pub/newlib/newlib-${pkgver}.tar.gz)
md5sums=('e5488f545c46287d360e68a801d470e8')
options=(!libtool !emptydirs !strip)

build() {
 cd ${srcdir}
 
 rm -rf build
 mkdir  build
 cd     build

 export CFLAGS="-O2"

 ../newlib-${pkgver}/configure \
   --target=arm-elf \
   --prefix=/usr \
   --enable-interwork \
   --enable-multilib \
   --with-gnu-as \
   --with-gnu-ld \
   --with-float=soft \
   --disable-nls || return 1

 make || return 1
}

package() {
 cd ${srcdir}/build || return 1

 make -j1 DESTDIR=${pkgdir} install || return 1

 rm -rf ${pkgdir}/usr/share/info

 install -D -m644 "${srcdir}/newlib-${pkgver}/COPYING.NEWLIB" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
