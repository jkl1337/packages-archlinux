# Adapted from cross-m68k-elf-gcc-base: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
# Contributor: Andreas Messer <andi@surveycorner.de>

pkgname=m68k-elf-gcc
pkgver=4.7.3
pkgrel=1
pkgdesc="The GNU Compiler Collection - Cross compiler for M68k target"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('m68k-elf-binutils' 'cloog' 'ppl' 'm68k-elf-newlib')
# m68k-elf-gcc is an superset of m68k-elf-gcc-base
conflicts=('m68k-elf-gcc-base')
provides=("m68k-elf-gcc-base=${pkgver}")
replaces=('cross-m68k-elf-gcc')
options=(!libtool !emptydirs !strip zipman docs)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('86f428a30379bdee0224e353ee2f999e')

build() {
  cd ${srcdir}/gcc-$pkgver

  export CPPFLAGS=""
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  [ $NOEXTRACT -eq 1 ] || rm -rf build
  mkdir build
  cd build

  [ $NOEXTRACT -eq 1 ] || ../configure --prefix=/usr \
	--target=m68k-elf \
	--host=$CHOST \
	--build=$CHOST \
        --enable-shared --disable-nls \
        --enable-languages=c,c++ \
        --enable-multilib \
	--with-arch=m68k \
	--with-local-prefix=/usr/lib/m68k-elf \
	--with-as=/usr/bin/m68k-elf-as \
        --with-ld=/usr/bin/m68k-elf-ld \
	--with-newlib \
        --with-sysroot=/usr/$CHOST/m68k-elf

  make all-gcc all-target-libgcc all-target-libstdc++-v3 || return 1
}

package() {
  cd ${srcdir}/gcc-$pkgver/build

  export CPPFLAGS=""
  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=${pkgdir} install-gcc install-target-libgcc install-target-libstdc++-v3 || return 1

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-$pkgver

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 
}
