# Maintainer: John K. Luebs <jkluebs@luebsphoto.com>

pkgname=sh-elf-binutils
pkgver=2.23.2
pkgrel=1
_xprefix=/usr
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=(i686 x86_64)
license=(GPL)
url="http://www.gnu.org/software/binutils/"
depends=('glibc>=2.17' 'zlib')
checkdepends=('dejagnu' 'bc')
options=('!libtool' '!distcc' '!ccache')
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-${pkgver}.tar.bz2{,.sig}
        binutils-2.23.2-texinfo-5.0.patch)
md5sums=('4f8fa651e35ef262edc01d60fb45702e'
         'dfde4428f08d91f309cdcfe92bf28d08'
         '34e439ce23213a91e2af872dfbb5094c')

prepare() {
  cd ${srcdir}/binutils-${pkgver}

  patch -p1 -i ${srcdir}/binutils-2.23.2-texinfo-5.0.patch

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  mkdir ${srcdir}/binutils-build
}

build() {
  cd ${srcdir}/binutils-build

  ${srcdir}/binutils-${pkgver}/configure --prefix=${_xprefix} \
	--program-prefix=sh-elf- \
	--enable-shared \
	--disable-multilib \
	--with-lib-path=${_xprefix}/lib/binutils/sh-elf \
	--disable-nls \
	--target=sh-elf \
	--host=$CHOST \
	--build=$CHOST \
	--with-sysroot=/usr/$CHOST/sh-elf \
#	--with-build-sysroot=/usr/i686-pc-linux-gnu/sh-elf/

  make configure-host
  make tooldir=${pkgdir}/${_xprefix}
}

check() {
  cd ${srcdir}/binutils-build

  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make LDFLAGS="" -k check || true
}

package() {
  cd ${srcdir}/binutils-build

  make prefix=$pkgdir/${_xprefix} tooldir=$pkgdir/${_xprefix} install

  mkdir -p $pkgdir/${_xprefix}/lib/binutils/sh-elf
  install -m644 ${srcdir}/binutils-${pkgver}/include/libiberty.h $pkgdir/${_xprefix}/lib/binutils/sh-elf
  install -m644 ${srcdir}/binutils-${pkgver}/include/demangle.h $pkgdir/${_xprefix}/lib/binutils/sh-elf

  rm -f $pkgdir/${_xprefix}/man/man1/{dlltool,nlmconv,windres}*

  rm -f $pkgdir/usr/bin/ar
  rm -f $pkgdir/usr/bin/as
  rm -f $pkgdir/usr/bin/ld
  rm -f $pkgdir/usr/bin/nm
  rm -f $pkgdir/usr/bin/objdump
  rm -f $pkgdir/usr/bin/ranlib
  rm -f $pkgdir/usr/bin/strip
  rm -f $pkgdir/usr/bin/objcopy
  rm -f $pkgdir/usr/lib/libiberty.a
  rm -rf $pkgdir/usr/share
}
