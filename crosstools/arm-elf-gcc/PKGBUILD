# Adapted from cross-arm-elf-gcc-base: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>
# Contributor: Andreas Messer <andi@surveycorner.de>

pkgname=arm-elf-gcc
pkgver=4.6.2
pkgrel=1
pkgdesc="The GNU Compiler Collection - Cross compiler for ARM target"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('arm-elf-binutils' 'cloog' 'ppl' 'arm-elf-newlib')
# arm-elf-gcc is an superset of arm-elf-gcc-base
conflicts=('arm-elf-gcc-base')
provides=("arm-elf-gcc-base=${pkgver}")
replaces=('cross-arm-elf-gcc')
options=(!libtool !emptydirs !strip zipman docs)
source=(ftp://gcc.gnu.org/pub/gcc/releases/gcc-${pkgver}/gcc-${pkgver}.tar.bz2)
md5sums=('028115c4fbfb6cfd75d6369f4a90d87e')

build() {
  cd ${srcdir}/gcc-$pkgver

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  [ $NOEXTRACT -eq 1 ] || rm -rf build
  mkdir build
  cd build

  [ $NOEXTRACT -eq 1 ] || ../configure --prefix=/usr \
	--target=arm-elf \
	--host=$CHOST \
	--build=$CHOST \
        --enable-shared --disable-nls \
        --enable-languages=c,c++ \
        --enable-multilib \
        --enable-interwork \
	--with-local-prefix=/usr/lib/arm-elf \
	--with-as=/usr/bin/arm-elf-as \
        --with-ld=/usr/bin/arm-elf-ld \
	--with-newlib \
        --with-float=soft \
        --with-sysroot=/usr/$CHOST/arm-elf

  make all-gcc all-target-libgcc all-target-libstdc++-v3 || return 1
}

package() {
  cd ${srcdir}/gcc-$pkgver/build

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  make DESTDIR=${pkgdir} install-gcc install-target-libgcc install-target-libstdc++-v3 || return 1

  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-$pkgver

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 
}
