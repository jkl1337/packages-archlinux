# Maintainer: John K. Luebs <jkluebs@luebsphoto.com>
# based on 'cross-arm-elf-gcc' by Andreas Messer <andi@surveycorner.de>

pkgname=cross-arm-none-eabi-gcc-linaro
provides=()
pkgver=4.6_2011.11
_pkgver=${pkgver//_/-}
pkgrel=jkl1
pkgdesc="The GNU Compiler Collection - Cross compiler for ARM target"
arch=(i686 x86_64)
license=('GPL' 'LGPL')
url="http://gcc.gnu.org"
#an installed libc/newlib is needed for libstdc++ compile
depends=('cross-arm-none-eabi-binutils>=2.21' 'cloog' 'ppl' 'cross-arm-none-eabi-newlib>=1.18.0')
# cross-arm-none-eabi-gcc is an superset of cross-arm-none-eabi-gcc-base
conflicts=('cross-arm-none-eabi-gcc-base')
provides=("cross-arm-none-eabi-gcc-linaro-base=${pkgver}" "cross-arm-none-eabi-gcc-base=4.6.1" "cross-arm-none-eabi-gcc=4.6.1")
options=(!libtool !emptydirs !strip zipman docs)
source=(gcc-linaro-${_pkgver}.tar.bz2)
sha256sums=('4c4288cc23bd435721c51b1aa9af6a09a581466206d5915c42889d18aa8f1f88')

build() {
  cd ${srcdir}/gcc-linaro-${_pkgver}

  export CFLAGS="-O2 -pipe"
  export CXXFLAGS="-O2 -pipe"

  rm -rf build
  mkdir build
  cd build

  ../configure --prefix=/usr \
               --target=arm-none-eabi \
               --disable-nls \
               --enable-languages=c,c++ \
               --enable-multilib \
               --enable-interwork \
               --with-local-prefix=/usr/lib/cross-arm \
               --with-as=/usr/bin/arm-none-eabi-as \
               --with-ld=/usr/bin/arm-none-eabi-ld \
               --with-newlib \
               --with-float=soft \
               --host=$CHOST \
               --build=$CHOST 

  make all-gcc all-target-libgcc all-target-libstdc++-v3 || return 1
}

package() {
  cd ${srcdir}/gcc-linaro-${_pkgver}/build
  
  make DESTDIR=${pkgdir} install-gcc install-target-libgcc install-target-libstdc++-v3 || return 1

  rm -rf $pkgdir/usr/share/gcc-4.6.2/python
  rm -f $pkgdir/usr/share/man/man7/fsf-funding.7*
  rm -f $pkgdir/usr/share/man/man7/gfdl.7*
  rm -f $pkgdir/usr/share/man/man7/gpl.7*
  rm -rf $pkgdir/usr/share/info
  rm -rf $pkgdir/usr/share/gcc-$pkgver

  cp -r  $pkgdir/usr/libexec/* $pkgdir/usr/lib/ && \
  rm -rf $pkgdir/usr/libexec 

}
